MODULE moduleInterfaceIono2Thermo

IMPLICIT NONE

PRIVATE  ! Set everything to private access, except those
         ! marked as public

PUBLIC :: INTERFACE__MID_LAT_IONOSPHERE_to_FIXED_GEO

SAVE

CONTAINS

!---------------------------------------------------------------------------

SUBROUTINE INTERFACE__MID_LAT_IONOSPHERE_to_FIXED_GEO(oplus, oplus_high_res_fixed) 

  IMPLICIT NONE
  INTEGER, PARAMETER :: NPTS2D = 44438
  INTEGER, PARAMETER :: NMP = 80  !# of magnetic longitude sectors
  INTEGER, PARAMETER :: NLP = 170 !# of field line grids in one longitude sector
  INTEGER, PARAMETER :: nheights = 183

  REAL(kind=8) :: dtotinv, factor, d, fac
  INTEGER :: i , iheight , iup, ido, ih, ip
  INTEGER :: l , m ,  mp , lp , in1, in2, lun2

  INTEGER :: MHIgh(90) , MLOw(90)
  INTEGER,DIMENSION(3,nheights,91,90) :: ii1, ii2, ii3, ii4
  INTEGER,DIMENSION(3,nheights,91,90) :: ii1_interface, ii2_interface,&
                                         ii3_interface, ii4_interface

  REAL(kind=8),Dimension(3,nheights,91,90) ::  facfac_interface,dd_interface
  INTEGER,DIMENSION(90) :: mlow_interface, mhigh_interface  

  REAL(kind=8),Dimension(npts2d,nmp) :: oplus  ! Input

  REAL(kind=8),Dimension(nheights,91,90) :: oplus_high_res_fixed  ! Output

  REAL(kind=8),Dimension(3) :: oplus_interpolated

!=======================================================================================

!g ---------------------------------------------------
!g  loop over all heights and the lats/longs......
!g ---------------------------------------------------
  lun2 = 101
  open(UNIT=lun2, file='GIP_Fixed_GEO_grid_lowres',  &
          form='formatted',status='unknown')

  read(lun2,*) facfac_interface
  read(lun2,*) dd_interface
  read(lun2,*) ii1_interface
  read(lun2,*) ii2_interface
  read(lun2,*) ii3_interface
  read(lun2,*) ii4_interface
  read(lun2,*) mlow_interface
  read(lun2,*) mhigh_interface
  close(lun2)


!   print *,'mlow=',mlow_interface
!   print *,'mhigh=',mhigh_interface

  do 100 iheight = 1, nheights
      do 200 l = 1, 90
          do 300 m = mlow_interface(l), mhigh_interface(l)

             !g ---------------------------------------------
             !g  Initialise our output parameters....
             !g ---------------------------------------------
              oplus_high_res_fixed(iheight,m,l) = 0.0
              dtotinv = 0.0

             !g -------------------------------------------------
             !g  The number of contributing flux-tube points
             !g  is always 3....
             !g -------------------------------------------------
              do 400 i = 1, 3

                 !g --------------------------------------
                 !g  The first interpolation uses facfac
                 !g --------------------------------------
                  factor = facfac_interface(i,iheight,m,l)
                  mp = ii1_interface(i,iheight,m,l)
                  lp = ii2_interface(i,iheight,m,l)
                  in2 = ii3_interface(i,iheight,m,l)
                  in1 = ii4_interface(i,iheight,m,l)

                  oplus_interpolated(i) = ((oplus(in2,mp)-oplus(in1,mp))*factor) &
                                          + oplus(in1,mp)

                 !g -----------------------------------------------------
                 !g Now we calculate the parameters at each point.....
                 !g -----------------------------------------------------

                  d = dd_interface(i,iheight,m,l)
                  dtotinv = (1./d) + dtotinv


                  oplus_high_res_fixed(iheight,m,l) = oplus_interpolated(i)/d &
                                            + oplus_high_res_fixed(iheight,m,l)

              400 ENDDO
          
              oplus_high_res_fixed(iheight,m,l) = oplus_high_res_fixed(iheight,m,l)/dtotinv

          300 ENDDO
      200 ENDDO
  100 ENDDO


  return


END SUBROUTINE INTERFACE__MID_LAT_IONOSPHERE_to_FIXED_GEO



END MODULE moduleInterfaceIono2Thermo
